// SPDX-License-Identifier: MIT

pragma solidity ^0.8.27;

import {IERC6372} from "@openzeppelin/contracts/interfaces/IERC6372.sol";
import {Math} from "@openzeppelin/contracts/utils/math/Math.sol";
import {SafeCast} from "@openzeppelin/contracts/utils/math/SafeCast.sol";
import {Checkpoints} from "@openzeppelin/contracts/utils/structs/Checkpoints.sol";
import {Time} from "@openzeppelin/contracts/utils/types/Time.sol";
import {ERC6372Utils} from "@openzeppelin/contracts/utils/ERC6372Utils.sol";
import {ERC7540Upgradeable} from "./ERC7540Upgradeable.sol";
import {Initializable} from "@openzeppelin/contracts/proxy/utils/Initializable.sol";

/**
 * @dev Time-delay fulfillment strategy for asynchronous deposits.
 *
 * Extends {ERC7540} with a deposit flow where requests become **permissionlessly claimable** after a
 * configurable waiting period. No privileged fulfiller is needed — once the delay elapses, the
 * controller (or any keeper) can claim. The exchange rate is computed at claim time using the vault's
 * live {convertToShares}.
 *
 * Production equivalents (redeem side):
 * https://github.com/beefyfinance/beefy-sonic/blob/main/contracts/BeefySonic.sol[BeefySonic],
 * https://github.com/MagmaStaking/contracts-public/blob/live/src/MagmaV2.sol[MagmaV2],
 * https://github.com/tangle-network/tnt-core/blob/main/src/staking/LiquidDelegationVault.sol[Tangle].
 *
 * Requests are tracked using {Checkpoints-Trace208}, storing cumulative deposit amounts keyed by
 * their maturity timepoint. The `requestId` returned by {requestDeposit} equals the absolute
 * timestamp at which the request becomes claimable (`clock() + depositDelay(controller)`).
 *
 * Override {depositDelay} to customize the waiting period (default: 1 hour) and {clock} to
 * change the time source (default: `block.timestamp`).
 *
 * NOTE: This module does not support temporary share custody through {_depositShareOrigin}. The constructor
 * tries to enforce that property, but the check may be insufficient if {_depositShareOrigin} reads from
 * storage that is not yet initialized when the parent's constructor runs
 */
abstract contract ERC7540DelayDepositUpgradeable is Initializable, ERC7540Upgradeable, IERC6372 {
    using SafeCast for uint256;
    using Checkpoints for Checkpoints.Trace208;

    /// @custom:storage-location erc7201:openzeppelin.storage.ERC7540DelayDeposit
    struct ERC7540DelayDepositStorage {
        mapping(address controller => Checkpoints.Trace208) _deposits;
        mapping(address controller => uint256) _claimedDeposits;
    }

    // keccak256(abi.encode(uint256(keccak256("openzeppelin.storage.ERC7540DelayDeposit")) - 1)) & ~bytes32(uint256(0xff))
    bytes32 private constant ERC7540DelayDepositStorageLocation = 0x875f11c9be3b0bc29ee7226c799cf8df570b2f9ddd654a05cf04995812760a00;

    function _getERC7540DelayDepositStorage() private pure returns (ERC7540DelayDepositStorage storage $) {
        assembly {
            $.slot := ERC7540DelayDepositStorageLocation
        }
    }

    /// @dev Triggered if {_depositShareOrigin} is not address(0), as this is not supported by this module.
    error ERC7540DelayInvalidDepositShareOrigin();

    function __ERC7540DelayDeposit_init() internal onlyInitializing {
        __ERC7540DelayDeposit_init_unchained();
    }

    function __ERC7540DelayDeposit_init_unchained() internal onlyInitializing {
        require(_depositShareOrigin() == address(0), ERC7540DelayInvalidDepositShareOrigin());
    }

    /// @inheritdoc IERC6372
    function clock() public view virtual returns (uint48) {
        return Time.timestamp();
    }

    /// @inheritdoc IERC6372
    // solhint-disable-next-line func-name-mixedcase
    function CLOCK_MODE() public view virtual returns (string memory) {
        return ERC6372Utils.timestampClockMode(clock);
    }

    /**
     * @dev Returns the delay duration before a deposit request becomes claimable. Defaults to 1 hour.
     *
     * NOTE: For any given `controller`, the maturity timepoint `clock() + depositDelay(controller)` MUST
     * be non-decreasing across successive {requestDeposit} calls. Overrides that shrink the delay faster
     * than `clock()` advances will cause new requests to revert until the previous maturity is reached.
     */
    function depositDelay(address /*controller*/) public view virtual returns (uint48) {
        return 1 hours;
    }

    /// @inheritdoc ERC7540Upgradeable
    function _isDepositAsync() internal pure virtual override returns (bool) {
        return true;
    }

    /**
     * @dev Pushes a new cumulative checkpoint at the maturity timepoint and delegates to
     * {ERC7540-_requestDeposit} with the timepoint as `requestId`.
     */
    function _requestDeposit(
        uint256 assets,
        address controller,
        address owner,
        uint256 /* requestId */
    ) internal virtual override returns (uint256) {
        ERC7540DelayDepositStorage storage $ = _getERC7540DelayDepositStorage();
        uint48 timepoint = clock() + depositDelay(controller);

        if (assets > 0) {
            uint256 latest = $._deposits[controller].latest();
            $._deposits[controller].push(timepoint, (assets + latest).toUint208());
        }

        return super._requestDeposit(assets, controller, owner, timepoint);
    }

    /**
     * @dev Consumes `assets` from claimable deposits, returns proportional shares (rounded down).
     *
     * Requirements:
     *
     * * {maxMint} must not be 0 for `controller`. Panics with division by zero otherwise.
     */
    function _consumeClaimableDeposit(uint256 assets, address controller) internal virtual override returns (uint256) {
        ERC7540DelayDepositStorage storage $ = _getERC7540DelayDepositStorage();
        uint256 shares = Math.mulDiv(assets, maxMint(controller), maxDeposit(controller), Math.Rounding.Floor);
        $._claimedDeposits[controller] += assets;
        return shares;
    }

    /// @dev Consumes `shares` from claimable deposits, returns proportional assets (rounded up).
    function _consumeClaimableMint(uint256 shares, address controller) internal virtual override returns (uint256) {
        ERC7540DelayDepositStorage storage $ = _getERC7540DelayDepositStorage();
        uint256 assets = Math.mulDiv(shares, maxDeposit(controller), maxMint(controller), Math.Rounding.Ceil);
        $._claimedDeposits[controller] += assets;
        return assets;
    }

    /**
     * @dev Returns the assets in Pending state for a specific `requestId` (timepoint).
     * A request is pending only if its timepoint is strictly in the future.
     */
    function _pendingDepositRequest(
        uint256 requestId,
        address controller
    ) internal view virtual override returns (uint256) {
        unchecked {
            uint48 timepoint = requestId.toUint48();
            return
                timepoint > clock()
                    ? _readyDepositAt(controller, timepoint) - _readyDepositAt(controller, timepoint - 1)
                    : 0;
        }
    }

    /**
     * @dev Returns the assets in Claimable state for a specific `requestId` (timepoint).
     * A request is claimable once its timepoint has elapsed and the assets haven't been claimed yet.
     */
    function _claimableDepositRequest(
        uint256 requestId,
        address controller
    ) internal view virtual override returns (uint256) {
        unchecked {
            uint48 timepoint = requestId.toUint48();
            return
                (timepoint == 0 || timepoint > clock())
                    ? 0
                    : _readyDepositAt(controller, timepoint) - _readyDepositAt(controller, timepoint - 1);
        }
    }

    /// @dev Returns the total claimable assets across all matured timepoints for `owner`.
    function _asyncMaxDeposit(address owner) internal view virtual override returns (uint256) {
        return _readyDepositAt(owner, clock());
    }

    /// @dev Returns the share-equivalent of {_asyncMaxDeposit} (rounded down).
    function _asyncMaxMint(address owner) internal view virtual override returns (uint256) {
        return _convertToShares(_readyDepositAt(owner, clock()), Math.Rounding.Floor);
    }

    /**
     * @dev Internal helper: fetch the amount that is expected to be claimable at a given timepoint, if any.
     * Any amount that has already been claimed is taken into consideration.
     */
    function _readyDepositAt(address owner, uint48 timepoint) internal view virtual returns (uint256) {
        ERC7540DelayDepositStorage storage $ = _getERC7540DelayDepositStorage();
        return Math.saturatingSub($._deposits[owner].upperLookupRecent(timepoint), $._claimedDeposits[owner]);
    }
}
